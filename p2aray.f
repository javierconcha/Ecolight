      SUBROUTINE P2ARAY(A,NR,NC,IDIM,IDFMT,TITLE)
C 
C     core routine on file p2aray.f (canned routine)
C
c     called by AMPZETA [MAIN->RADAMPS->AMPZETA->P2ARAY]
c               QASKY   [MAIN->RADAMPS->INISHAMP->QASKY->P2ARAY]
c               RADAMPS [MAIN->RADAMPS->P2ARAY]
c               SGEFS   [SGEFS is called by AMPW, AMPZETA, and INFBOTM]
c               WRFDISC [MAIN->WRFDISC->P2ARAY]
C
C     THIS ROUTINE PRINTS OUT AN ARRAY A OF NR ROWS AND NC COLUMNS ON ANY 
C     OF A NUMBER OF FORMATS.  IDIM IS THE ROW DIMENSION OF A IN THE
C     CALLING PROGRAM.  THE VALUE OF IDFMT SPECIFIES THE FORMAT:  
C 
C     IDFMT = 1 FOR 10F12.4 
C             2 FOR 1P,10E12.4
C             3 FOR 10I12 
C             4 FOR 12A10 
C             5 FOR 1P,6E20.8
C             6 FOR 20F5.1
C             7 FOR 1P,5E25.15
C 
C     THE ARRAY IS PARTITIONED BY COLUMNS INTO BLOCKS, AND ROWS AND 
C     COLUMNS ARE NUMBERED.  THE CHARACTER STRING TITLE CONTAINS ANY
C     DESIRED TITLE (UP TO 130 CHARACTERS FOR PRINTER OUTPUT).
C 
      DIMENSION A(IDIM,NC)
      CHARACTER TITLE*(*) 
C 
C     SET UP THE PROPER FORMATS 
C 
      KSIZE = 10
      ASSIGN 910 TO IFMT1 
      IF(IDFMT.EQ.1) THEN 
      ASSIGN 11 TO IFMT2
      ELSEIF(IDFMT.EQ.2) THEN 
      ASSIGN 21 TO IFMT2
      ELSEIF(IDFMT.EQ.3) THEN 
      ASSIGN 31 TO IFMT2
      ELSEIF(IDFMT.EQ.4) THEN 
      KSIZE = 12
      ASSIGN 912 TO IFMT1 
      ASSIGN 41 TO IFMT2
      ELSEIF(IDFMT.EQ.5) THEN 
      KSIZE= 6
      ASSIGN 906 TO IFMT1 
      ASSIGN 51 TO IFMT2
      ELSEIF(IDFMT.EQ.6) THEN 
      KSIZE = 20
      ASSIGN 920 TO IFMT1 
      ASSIGN 61 TO IFMT2
      ELSEIF(IDFMT.EQ.7) THEN 
      KSIZE = 5 
      ASSIGN 905 TO IFMT1 
      ASSIGN 71 TO IFMT2
      ELSE
      write(10,100) IDFMT
      RETURN
      ENDIF 
C 
C     PARTITION ARRAY 
C 
      KMANY = (((NC-1)/KSIZE) + 1)*KSIZE
      IBLOCK = 0
      NBLOCK = 1
      IF(NR.LE.25) NBLOCK = 60/(NR+4) 
C 
C     PRINT ARRAY 
C 
      DO L=KSIZE,KMANY,KSIZE
      L1 = L - (KSIZE - 1)
      L2 = L
      IF(L.GE.KMANY) L2 = NC
C 
      DO I=1,NR
      IF(MOD(I-1,50).NE.0) GO TO 210
C 
      IF(IBLOCK.EQ.0 .OR.IBLOCK.GE.NBLOCK) THEN 
C 
C     PRINT TITLE AND COLUMN HEADINGS IF NEW PAGE 
      IBLOCK = 1
      write(10,110) TITLE
      write(10,IFMT1) (K,K=L1,L2)
      ELSE
C 
C     PRINT COLUMN HEADINGS FOR A NEW BLOCK 
      IBLOCK = IBLOCK + 1 
      write(10,IFMT1) (K,K=L1,L2)
      ENDIF 
C 
C     WRITE A LINE OF DATA
 210  write(10,IFMT2) I,(A(I,J),J=L1,L2)
      end do
      end do
C 
      RETURN
C 
C     FORMATS 
C 
C     IFMT1 FOR COLUMN LABELS 
  905 FORMAT(' ',//10X,5I25)
  906 FORMAT(' ',//10X,6I20)
  910 FORMAT(' ',//10X,10I12)
  912 FORMAT(' ',//10X,12I10)
  920 FORMAT(' ',//10X,20I5)
C 
C     IFMT2 FOR DATA
   11 FORMAT(' ',I9,10F12.4)
   21 FORMAT(' ',I9,1P,10E12.3)
   31 FORMAT(' ',I9,10I12)
   41 FORMAT(' ',I9,12A10)
   51 FORMAT(' ',I9,1P,6E20.8)
   61 FORMAT(' ',I9,20F5.1)
   71 FORMAT(' ',I9,1P,5E25.15)
C 
  100 FORMAT(//' INVALID FORMAT OPTION IN P2ARAY, IDFMT =',I5) 
  110 FORMAT(//,A) 
      END 
